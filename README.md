<img src="https://github.com/DmGladkih/1C-code-examples/blob/Task-2/1.jpg" height="800"/>

Процедура ОбработкаПроведения(Отказ, Режим) 
	
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.Записать();
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движения.Продажи.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродажаТовараСписокТоваров.Товар КАК Товар,
	|	СУММА(ПродажаТовараСписокТоваров.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_ТоварыДокумента
	|ИЗ
	|	Документ.ПродажаТовара.СписокТоваров КАК ПродажаТовараСписокТоваров
	|ГДЕ
	|	ПродажаТовараСписокТоваров.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПродажаТовараСписокТоваров.Товар
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ОстаткиТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ВТ_ТоварыДокумента.Товар КАК ТоварВДокументе,
	|	ВТ_ТоварыДокумента.Количество КАК КоличествоВДокументе,
	|   ВТ_ТоварыДокумента.Товар.Представление КАК ПредставлениеТовараВДокументе
	|ИЗ
	|	ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваров.Остатки(
	|				&МоментВремени,
	|				Склад = &Склад
	|					И Товар В
	|						(ВЫБРАТЬ
	|							ВТ_ТоварыДокумента.Товар КАК Товар
	|						ИЗ
	|							ВТ_ТоварыДокумента КАК ВТ_ТоварыДокумента)) КАК ОстаткиТоваровОстатки
	|		ПО ВТ_ТоварыДокумента.Товар = ОстаткиТоваровОстатки.Товар";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоВДокументе > Выборка.КоличествоОстаток Тогда
			
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("Недостаточно товара %1 в количестве %2", Выборка.ПредставлениеТовараВДокументе,
			Выборка.КоличествоВДокументе - Выборка.КоличествоОстаток);
			Сообщение.Сообщить(); 
			
		КонецЕсли; 
		
		Если Отказ = Истина Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Движение = Движения.ОстаткиТоваров.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Товар = Выборка.ТоварВДокументе;
		Движение.Склад = Склад;
		Движение.Количество = Выборка.КоличествоВДокументе;
		
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Склад = Склад;
		Движение.Товар = Выборка.ТоварВДокументе;
		Движение.Количество = Выборка.КоличествоВДокументе;

		
	КонецЦикла;
	
КонецПроцедуры   



